* ----------------------------------------------------------- *
* Function ...: First Sunday of the Month
* author .....: Ralph Zbrog
* https://tech.forums.softwareag.com/t/first-sunday-of-the-month/275229/5
* ----------------------------------------------------------- *
DEFINE DATA LOCAL
1 #DATE (N6)       1 REDEFINE #DATE
  2 #MMYY (A6)
1 #DATE-A (A8)
1 #FIRST (D)
1 #DAY (A1)        1 REDEFINE #DAY
  2 #D (N1)
1 #SUNDAY (D)
1 #DATA (A253/4)   1 REDEFINE #DATA
  2 FILLER 33X
  2 #DTFORM (A1)
END-DEFINE
CALLNAT 'USR1005N' #DATA (*)
*
REPEAT
  INPUT 'Date (mmyyyy):' #DATE (SG=F AD=M)
  COMPRESS '01'
           #MMYY
      INTO #DATE-A LEAVING NO SPACE
  MOVE EDITED #DATE-A       TO #FIRST (EM=DDMMYYYY)
  MOVE EDITED #FIRST (EM=O) TO #DAY
  IF  #DTFORM = 'U'
    THEN
      IF  #DAY = '1'
        THEN
          ASSIGN #SUNDAY = #FIRST
        ELSE
          ASSIGN #SUNDAY = #FIRST + 7 - #D + 1
      END-IF
    ELSE
      IF  #DAY = '7'
        THEN
          ASSIGN #SUNDAY = #FIRST
        ELSE
          ASSIGN #SUNDAY = #FIRST + 7 - #D
      END-IF
  END-IF
  DISPLAY #FIRST  (EM=MM-DD-YYYY)
          #DAY
          #DTFORM
          #SUNDAY (EM=MM-DD-YYYY)
END-REPEAT
END
